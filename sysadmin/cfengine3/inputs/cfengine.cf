#
# cfengine.cf
#

bundle agent cfengine
{
vars:

    "component" slist   => { "cf-monitord", "cf-serverd" };

commands:

    "$(sys.workdir)/bin/$(component)"
        ifvarclass => canonify("start_$(component)");

files:

    "$(g.crontab)"
        comment     => "Add cfengine as a cron job, or check for cfengine 2 jobs to replace",
        create      => "true",
        edit_line   => upgrade_cfexecd;

    "$(sys.workdir)/outputs"
        comment         => "Garbage collection issues",
        delete          => tidy,
        depth_search    => recurse("inf"),
        file_select     => days_old("3");

processes:

    exec_fix::
    "cron" signals                  => { "hup" };

    "$(component)" restart_class    => canonify("start_$(component)");
}

######################################################################
# Anomaly monitoring
######################################################################

bundle agent anomalies
{
reports:

    rootprocs_high_dev2::
    "RootProc anomaly high 2 dev on $(mon.host) at $(mon.env_time)
    measured value $(mon.value_rootprocs) av $(mon.av_rootprocs) pm $(mon.dev_rootprocs)"
        showstate   => { "rootprocs" };

    entropy_www_in_high&anomaly_hosts.www_in_high_anomaly::
    "HIGH ENTROPY Incoming www anomaly high anomaly dev!! on $(mon.host) at $(mon.env_time)
    - measured value $(mon.value_www_in) av $(mon.av_www_in) pm $(mon.dev_www_in)"
        showstate   => { "incoming.www" };

    entropy_www_in_low.anomaly_hosts.www_in_high_anomaly::
    "LOW ENTROPY Incoming www anomaly high anomaly dev!! on $(mon.host) at $(mon.env_time)
    - measured value $(svalue_www_in) av $(av_www_in) pm $(dev_www_in)"
        showstate   => { "incoming.www" };

    entropy_tcpsyn_in_low.anomaly_hosts.tcpsyn_in_high_dev2::
    "Anomalous number of new TCP connections on $(mon.host) at $(mon.env_time)
    - measured value $(mon.value_tcpsyn_in) av $(mon.av_tcpsyn_in) pm $(mon.dev_tcpsyn_in)"
        showstate   => { "incoming.tcpsyn" };

    entropy_dns_in_low.anomaly_hosts.dns_in_high_anomaly::
    "Anomalous (3dev) incoming DNS packets on $(mon.host) at $(mon.env_time)
    - measured value $(mon.value_dns_in) av $(av_dns_in) pm $(mon.dev_dns_in)"
        showstate   => { "incoming.dns" };

    entropy_dns_in_low.anomaly_hosts.udp_in_high_dev2::
    "Anomalous (2dev) incoming (non-DNS) UDP traffic on $(mon.host) at $(mon.env_time)
    - measured value $(mon.value_udp_in) av $(mon.av_udp_in) pm $(mon.dev_udp_in)"
        showstate   => { "incoming.udp" };

    anomaly_hosts.icmp_in_high_anomaly.!entropy_icmp_in_high::
    "Anomalous low entropy (3dev) incoming ICMP traffic on $(mon.host) at $(mon.env_time)
    - measured value $(mon.value_icmp_in) av $(mon.av_icmp_in) pm $(mon.dev_icmp_in)"
        showstate   => { "incoming.icmp" };
}

######################################################################

bundle edit_line upgrade_cfexecd
{
classes:

    "exec_fix" not  => regline(".*cf-execd.*", "$(edit.filename)");

insert_lines:

    exec_fix::
    "0,5,10,15,20,25,30,35,40,45,50,55 * * * * $(g_update.cf_bin_dir)/cf-execd -F";

replace_patterns:

    "cfexecd" replace_with  => value("cf-execd");

reports:

    exec_fix::
    "Added a 5 minute schedule to crontabs";
}

