#
# kerberos.cf
#

bundle agent kerberos
{
vars:
    "match_package" slist  => {
                                "gnutls-bin",
                                "krb5-admin-server",
                                "krb5-clients",
                                "krb5-kdc",
                                "krb5-user"
                               };

packages:
    "${match_package}"
        package_method  => apt,
        package_policy  => "add";

files:

    "/etc/krb5.conf"
        comment     => "Kerberos 5 configuration file",
        classes     => if_repaired("krb5_conf_changed"),
        copy_from   => local_cp("$(sys.workdir)/inputs/etc/krb5.conf"),
        create      => "true",
        perms       => mog("0644", "root", "root");

    "/etc/krb5kdc/kdc.conf"
        comment     => "Kerberos v5 key server configuration file",
        classes     => if_repaired("kdc_conf_changed"),
        copy_from   => local_cp("$(sys.workdir)/inputs/etc/krb5kdc/kdc.conf"),
        create      => "true",
        perms       => mog("0644", "root", "root");

    "/etc/krb5kdc"
        comment     => "Kerberos v5 key server configuration directory",
        perms       => mog("0600", "root", "root");

commands:

    krb5_conf_changed::
    "/usr/sbin/invoke-rc.d krb5-admin-server restart"
        comment => "Restart Kerberos v5 admin server";

    krb5_conf_changed|kdc_conf_changed::
    "/usr/sbin/invoke-rc.d krb5-kdc restart"
        comment => "Restart Kerberos v5 key server";
}

