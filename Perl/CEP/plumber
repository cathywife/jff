#!/usr/bin/env perl
use strict;
use warnings;
use Data::Dumper;
use Getopt::Long;
use HTTP::Response;
use JSON;
use Log::Log4perl qw(:easy);
use POE qw(Component::Server::TCP Filter::HTTPD);
use Try::Tiny;


my $g_port = 9999;

#
# Initialize Log4perl
#
Log::Log4perl->easy_init({
        level   => $DEBUG,
        layout  => "[%d] %-5p (%P) %F{1}:%-4L - %m%n",
        #file   => ">plumber.log",
    });


#
# Process command line options
#
GetOptions("port=i"     => \$g_port);
die "Bad port $g_port\n" if $g_port <= 0 || $g_port > 65535;

INFO "listen on port $g_port";

#
# Start server
#
POE::Component::Server::TCP->new(
    Port                => $g_port,
    Started             => \&on_master_started,
    ClientFilter        => 'POE::Filter::HTTPD',
    ClientConnected     => \&on_client_connected,
    ClientDisconnected  => \&on_client_disconnected,
    ClientInput         => \&on_client_input,
);

POE::Kernel->run;
exit;


######################################################################
sub on_client_connected {
    my ($kernel, $heap) = @_[KERNEL, HEAP];

    INFO "client connected from $heap->{remote_ip} port $heap->{remote_port}";

    register_HUP_INT_TERM_handler($kernel);
}


sub on_client_disconnected {
    my $heap = $_[HEAP];

    INFO "client disconnected from $heap->{remote_ip} port $heap->{remote_port}";
}


sub on_client_input {
    my ($heap, $kernel, $request) = @_[HEAP, KERNEL, ARG0];

    # It's a response for the client if there was a problem
    if ($request->isa("HTTP::Response")) {
        my $response = $request;

        $request = $response->request;
        WARN "bad request: ", $request->message if $request;

        $_[HEAP]{client}->put($response);
        $_[KERNEL]->yield("shutdown");
        return;
    }

    try {
        my $response = handle_request($request);
        $heap->{client}->put($response);
    } catch {
        ERROR "caught exception when handle request: $_";
        $kernel->yield("shutdown");
    };
}


sub handle_request {
    my ($request) = @_;

    my $response = HTTP::Response->new(200);
    my $content = scalar(localtime);
    $response->push_header("Content-type", "text/plain");
    $response->push_header("Content-length", length($content));
    $response->content($content);

    return $response;
}


sub sig_HUP_INT_TERM {
    my ($kernel, $heap, $sig) = @_[KERNEL, HEAP, ARG0];

    my $who = "listener";
    if (! exists $heap->{listener}) {
        $who = "client";
    }

    WARN "$who got signal $sig!";

    $kernel->yield("shutdown");
    $kernel->sig_handled();
}


sub register_HUP_INT_TERM_handler {
    my ($kernel) = @_;

    $kernel->state("sig_HUP_INT_TERM", \&sig_HUP_INT_TERM);
    $kernel->sig(HUP    => "sig_HUP_INT_TERM");
    $kernel->sig(INT    => "sig_HUP_INT_TERM");
    $kernel->sig(TERM   => "sig_HUP_INT_TERM");
}


sub on_master_started {
    my $kernel = $_[KERNEL];

    register_HUP_INT_TERM_handler($kernel);
}

